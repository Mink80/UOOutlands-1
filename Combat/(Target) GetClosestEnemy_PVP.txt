/////////////////////////////////////////////
// UO Steam Macro for PVP
// for Outlands
//
// Greetings and thank you for your interest in this script!
// This macro is *primarily* for PVP, and is a "general purpose"
// script for most players on the shard doing PVP. Its primary
// purpose is to scan around you for the closest target, sets
// that target to your 'enemy' alias.
//
// Setting your 'enemy' alias will not attack that target. You need to
// combine this with another key, that drops a harmful spell or attack
// on your 'enemy' target.
//
// How to use:
// ===========
// 1) Run script to set your 'enemy' alias
// 2) Run another script or keybind to attack/target 'enemy'
//
// Versions:
// =========
// 8.28.2019 - complete rewrite to set your 'enemy' alias AND highlight it.
// 8.30.2019 - big help lift from ~roxya~ to get the highlighting to work!
//
// >> COPY BELOW >> 
/////////////////////////////////////////////

@createlist 'enemies'
@clearlist 'enemies'

//
// Scan for targets
//
for 0 to 20
    if murderer 'self'
        @getenemy 'murderer' 'innocent' 'enemy' 'criminal' 'gray' 'humanoid' 'next'
    else
        @getenemy 'murderer' 'enemy' 'criminal' 'gray' 'humanoid' 'next'
    endif
    if @findobject 'enemy'
        @pushlist! 'enemies' 'enemy'
    endif
endfor

//
// Create a distances list, seeding from 1
// 
@createlist 'distances'
@clearlist 'distances'
pushlist 'distances' 1
pushlist 'distances' 2
pushlist 'distances' 3
pushlist 'distances' 4
pushlist 'distances' 5
pushlist 'distances' 6
pushlist 'distances' 7
pushlist 'distances' 8
pushlist 'distances' 9
pushlist 'distances' 10
pushlist 'distances' 11
pushlist 'distances' 12
pushlist 'distances' 13
pushlist 'distances' 14
pushlist 'distances' 15

//
// Sort by distance
//  
@createlist 'closest'
@clearlist 'closest'
for 0 to 'distances'
    if list 'closest' == 0
        for 0 to 'enemies'
            if @inrange enemies[] distances[] and not dead enemies[]
                pushlist 'closest' 'enemies[]'
                break
            endif
        endfor
    else
        break
    endif
endfor

//
// check if no targets
//
if list 'closest' > 0
    setalias 'enemy' 'closest[0]'
    setalias 'last' 'closest[0]'
else
    headmsg "No targets" 44
    stop
endif