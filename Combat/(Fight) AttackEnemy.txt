/////////////////////////////////////////////
// UO Steam Attack Enemy Macro
// for Outlands
// by Jewele
//
// General purpose attack macro :
// ==============================
// * Plays dress macro to re-equip your weapons
// * Drops a held spell onto your enemy (if in range)
// * Active spell prompts will be held if enemy out of range
// * Puts you into war mode if enemy nearby
//
// This macro is best used in conjunction with another macro
// which sets your 'enemy' alias.
//   
//   For example:
// 
//   [MOUSE SCROLL DOWN] - get nearest enemy
//   [MOUSE SCROLL UP] - attack enemy (this macro)
//
//  TIP! Macros like these work well if you have a spell up and
//       are chasing someone. You can run the macro over and over
//       again to drop the spell, but it won't drop unless they
//       are in range.
//
//  TIP! If you are an archer, pre-cast explosion, run this macro
//       as it will arm your bow and begin attacking your enemy. 
//       If your enemy is in spell range, the spell will also be
//       dropped.
//
//  TIP! If you don't have 100 wrestling, you want to make sure you 
//       arm your weapon ASAP - as you are very vulerable without
//       it. This is why this macro makes sure to rearm your loadout.
//
// Requires:
// =========
// 1. You to have an enemy set
// 2. You have recorded your equipment in a dress profile
//
// When to use:
// ============
// * You want to drop a spell on an enemy in range
// * You want to attack an enemy
//
// How to use:
// ===========
// 1. Save your current equipment as 'default'
//    in dress macro. 
// 2. Run script any time to attack your current target.
//
// COPY HERE >>
/////////////////////////////////////////////

//
// run dress macro
// 
// On Outlands you can cast a spell and equip your weapons
// while the spell is still active, which makes this part
// quite nice for caster templates.
//
dress 'default'
while dressing
endwhile

//
// check 'enemy' alias and attack
//
if @findobject 'enemy'

    attack! 'enemy'
    setalias 'last' 'enemy'

    // 
    // drop queued spell if enemy in range
    //
    while targetexists 
        if @inrange 'enemy' 15 and not dead 'enemy'
            target! 'enemy' 1
            pause 1000
            break
        else
            headmsg "too far!" 44
        endif    
        pause 500    
    endwhile

    //
    // set war mode
    //
    if not war 'self'
       warmode 'on'
    endif

else
    headmsg "No targets" 44
endif

@clearusequeue
@cancelautotarget